# Copyright (c) 2025 Antmicro <www.antmicro.com>
#
# SPDX-License-Identifier: Apache-2.0

if(CONFIG_ZEPHYR_EXECUTORCH_MODULE)

    # Turning of dynamic linking tables (without it the function measuring model inference time crashes)
    zephyr_compile_options(-fno-plt)

    # Turning off exceptions to make the program faster
    zephyr_compile_options(-fno-exceptions)

    include(${ZEPHYR_CURRENT_MODULE_DIR}/tools/cmake/common/preset.cmake)

    # Including ExecuTorch Zephyr preset, it disables all backends and additional options, turns of portable_ops_lib (a library of portable inference operators).
    include(${ZEPHYR_CURRENT_MODULE_DIR}/tools/cmake/preset/zephyr.cmake)

    # We need to disable dynamic linking (on some platforms it tries to link libdl if we don't disable this explicitly).
    set(EXECUTORCH_USE_DL OFF)

    # This option will cause a separate executable file to be built, which we don't need (we only need to link ExecuTorch as a library).
    set(EXECUTORCH_BUILD_EXECUTOR_RUNNER OFF)

    # Disabling some unneeded extensions
    set(EXECUTORCH_BUILD_EXTENSION_EVALUE_UTIL OFF)
    set(EXECUTORCH_BUILD_EXTENSION_RUNNER_UTIL OFF)

    # We are not using XNNPACK (because it has no support for Zephyr), so we disable everything related to it.
    set(EXECUTORCH_XNNPACK_ENABLE_KLEIDI OFF)
    set(EXECUTORCH_XNNPACK_SHARED_WORKSPACE OFF)

    # Posix PAL (Platform Abstraction Layer) seems to depend on a specific implementation of POSIX (it requires access to _impure_ptr global variable), so we need to use the minimal one.
    set(EXECUTORCH_PAL_DEFAULT "minimal")

    # If model path is given, we give it to ExecuTorch's cmake to infer what portable_ops_lib operators will be needed, select them and put into a separate target: executorch_selected_kernels
    # Model path has to be relative to Kenning Zephyr Runtime's project root.
    if(CONFIG_KENNING_MODEL_PATH)
        set(EXECUTORCH_SELECT_OPS_MODEL ${APPLICATION_CONFIG_DIR}/../${CONFIG_KENNING_MODEL_PATH})
    endif()

    # ExecuTorch library has it's source code directly in the project root (not in ,,src" directory or anything similar, so we have to use this workaround).
    include_directories(${ZEPHYR_CURRENT_MODULE_DIR}/../)

    # We cannot link ExecuTorch's source code files directly, because some of those are generated by CMake, so we have to use the provided ExecuTorch CMakeLists.txt file.
    add_subdirectory(${ZEPHYR_CURRENT_MODULE_DIR} ${APPLICATION_CONFIG_DIR}/../build/executorch)

    # Linking ExecuTorch core files
    zephyr_link_libraries(
        executorch_core
    )

    # If operators were selected based on model, we link the target with selected operators. Otherwise we link the entire portable_ops_lib
    if(CONFIG_KENNING_MODEL_PATH)
        zephyr_link_libraries(
        executorch_selected_kernels
    )
    else()
        zephyr_link_libraries(
        portable_ops_lib
    )
    endif()

endif()
